apply plugin: 'com.android.application'

/*
The arcore aar library contains the native shared libraries.  These are
extracted before building to a temporary directory.
 */
def external_libs = "${buildDir}/external_libs"

// Create a configuration to mark which aars to extract .so files from
configurations { natives }

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "de.tuilmenau.saltwater"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14", "-frtti", "-fexceptions"
                arguments "-DANDROID_STL=c++_static", "-DEXTERNAL_LIBPATH=${external_libs}/jni"
            }
        }
        ndk {
            abiFilters 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['../../../../extern/assimp/lib', '../../../../extern/devil/lib', '../../../../extern/tinyxml2/lib', 'build/intermediates/cmake/debug/obj/arm64-v8a']
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    // ARCore library
    implementation 'com.google.ar:core:1.6.0'
    natives 'com.google.ar:core:1.6.0'

    // Android core
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:support-v13:27.0.2'
}

// Extracts the shared libraries from aars in the natives configuration.
// This is done so that NDK builds can access these libraries.
task extractNativeLibraries() {
    doFirst {
        configurations.natives.files.each { f ->
            copy {
                from zipTree(f)
                into external_libs
                include "jni/**/*"
            }
        }
    }
}

tasks.whenTaskAdded {
    task-> if (task.name.contains("external") && !task.name.contains("Clean")) {
        task.dependsOn(extractNativeLibraries)
    }
}