apply plugin: 'com.android.application'

/*
The arcore aar library contains the native shared libraries.  These are
extracted before building to a temporary directory.
 */
def external_libs = "${buildDir}/external_libs"

// Create a configuration to mark which aars to extract .so files from
configurations { natives }

def setVersionName = { ->
    if (project.hasProperty("androidVersionName")) {
        println("Replacing versionName with supplied build parameter: " +
                "$androidVersionName")
        return androidVersionName
    } else {
        return "1.0"
    }
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "de.tuilmenau.saltwater"
        minSdkVersion 28
        targetSdkVersion 29
        versionCode 1
        versionName setVersionName()
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17", "-frtti", "-fexceptions"
                arguments "-DANDROID_STL=c++_static", "-DEXTERNAL_LIBPATH=${external_libs}/jni"
            }
        }
        ndk {
            abiFilters 'arm64-v8a'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['../../../../extern/assimp/lib', '../../../../extern/devil/lib', '../../../../extern/tinyxml2/lib', 'build/intermediates/cmake/debug/obj/arm64-v8a']
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    buildToolsVersion = '30.0.1'
}

dependencies {
    // ARCore library
    implementation 'com.google.ar:core:1.18.0'
    natives 'com.google.ar:core:1.18.0'

    // Android core
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}

// Extracts the shared libraries from aars in the natives configuration.
// This is done so that NDK builds can access these libraries.
task extractNativeLibraries() {
    outputs.upToDateWhen { false }
    doFirst {
        configurations.natives.files.each { f ->
            copy {
                from zipTree(f)
                into external_libs
                include "jni/**/*"
            }
        }
    }
}

tasks.whenTaskAdded {
    task-> if (task.name.contains("external") && !task.name.contains("Clean")) {
        task.dependsOn(extractNativeLibraries)
    }
}